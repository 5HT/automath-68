
use crate::ast::Henk;
grammar; // AUT-68 = U | I | [ I : PTS ) PTS | ( I : PTS ) PTS | PTS PTS | ( PTS )

Name:      String = { r"[a-zA-Z][a-zA-Z\d]*" => <>.to_string() };
Universe:    Henk = { <n:r"\*[\d]+"> => Henk::Universe(i64::from_str_radix(n,10).unwrap()), <n:r"\*"> => Henk::Universe(0), };
Variable:    Henk = { <n:Name> => Henk::Variable(n), };
Unit:        Henk = { <l:"("> <e:Expr> <r:")"> => e, };
Lambda:      Henk = { <l:"("> <v:Name> <s:":"> <t:Expr> <r:")"> <e:LExpr> => Henk::Lambda(v,Box::new(t),Box::new(e)), };
Forall:      Henk = { <l:"["> <v:Name> <s:":"> <t:Expr> <r:"]"> <e:LExpr> => Henk::Forall(v,Box::new(t),Box::new(e)), };
LExpr:       Henk = { Variable, Unit, Lambda, Forall };
RExpr:       Henk = { Variable, Unit, Application, Arrow };
Application: Henk = { <l:LExpr> <r:RExpr> => Henk::Application(Box::new(l),Box::new(r)), };
Arrow:       Henk = { <t:LExpr> <a:">"> <e:Expr> => Henk::Forall("".to_string(),Box::new(t),Box::new(e)), };
pub    Expr: Henk = { #[precedence(level="0")] Universe,
                      #[precedence(level="1")] #[assoc(side="right")] Arrow,
                      #[precedence(level="2")] #[assoc(side="left")] Application,
                      #[precedence(level="3")] Variable,
                      #[precedence(level="4")] Unit,
                      #[precedence(level="5")] #[assoc(side="left")] Lambda,
                      #[precedence(level="6")] #[assoc(side="left")] Forall,
                     };
